plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id "com.modrinth.minotaur" version "1.2.1"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}"
group = project.maven_group

def secretsProperties = new Properties()
file('secrets.properties').withInputStream { secretsProperties.load(it) }

minecraft {
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"

	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from "LICENSE"
}

curseforge {
  apiKey = secretsProperties['curseApiKey']
  project {
    id = project.curseforge_id
    changelog = project.changelog
    releaseType = 'release'
    addGameVersion project.minecraft_version
	relations {
		requiredDependency 'fabric-api'
	}

    mainArtifact(remapJar) {
      displayName = "Better Signs & Frames $project.mod_version"
    }
  }
  options {
	  forgeGradleIntegration = false
  }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload) {
    token = secretsProperties['modrinthToken']
    projectId = project.modrinth_id
	versionName = "Better Signs & Frames $project.mod_version for MC $project.minecraft_version"
    versionNumber = "mc$project.minecraft_version-v$project.mod_version"
	versionType = 'RELEASE'
    uploadFile = remapJar
	changelog = project.changelog
    addGameVersion project.minecraft_version
    addLoader 'fabric'
}
